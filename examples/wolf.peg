:space = ' '+
:equals = ' '* '=' ' '*
:comma = ' '* ',' ' '*
:str = '"' (^[\\"\n] | [\\] '"')* '"' | "'" (^[\\'\n] | [\\] "'")* "'"

:comment = '//' '-'? ^[\n]*
// :comment _= '//' '-'? ^[\n]*

:not_paren = (:str | ^[()\n])+
:not_bracket = (:str | ^[{}\n])+
:not_double_bracket = (^[\\{}\n] | '{' ^[\\{\n] | '}' ^[\\}\n] | [\\] ^[\\\n]{1,2})+

:identifier = #word+
:tag_identifier = (#word | '-')+
:at_identifier = '@' :tag_identifier
:plus_identifier = '+' :tag_identifier
// this can handle *one level deep* of balanced parens within the name
:attribute_name = (^[\s,'"=()] | '(' ^[\s,'"=()]* ')')+ | :str

:id_identifier = '#' :tag_identifier
:class_identifier = '.' :tag_identifier

{ :indent, :deindent, :indent_continue } = use 'kreia/dist/virtual_lexers/IndentationLexer'
// { :indent, :deindent, :indent_continue } = use 'kreia/dist/virtual_lexers/IndentationLexerWithIndentedBlock' with :


wolf = @lines[entity | comment]

entity =
	|
		entity_descriptor (
			| ':' :space entity
			| :indent wolf :deindent
			| ' ' text+
		)?
	|
		'|' (
			| :indent @lines[text+] :deindent
			| ' ' text+
		)

entity_descriptor =
	| tag attributes? '/'?
	| :plus_identifier attributes?
	| :at_identifier (:space? '(' paren_code+ ')')?

tag =
	| :tag_identifier meta*
	| meta+

meta =
	| :id_identifier
	| :class_identifier
	| '.' code_segment
	| '#' code_segment

// the difference between enforcing trailing commas always or never
// | :indent @lines[attribute_line] :deindent :indent_continue
attributes =
	'('
	| :indent @lines[attribute_line :comma] :deindent :indent_continue
	| :space? attribute_line :space?
	')'

attribute_line = attribute (:comma attribute)*

attribute =
	:attribute_name
	?
		:equals
		| :identifier
		| str
		| code_segment

str = :str

code_segment = '{' code* '}'

code =
	| '{' code* '}'
	| :str
	| :not_bracket

paren_code =
	| '(' paren_code* ')'
	| :not_paren

text =
	| '{{' code* '}}'
	| :not_double_bracket

comment = :comment

// @many_separated[$body, $separator] = $body ($separator $body)*
@lines[$content] = $content (:indent_continue $content)*
